CompUnit      ::= [CompUnit] FuncDef;

FuncDef       ::= FuncType IDENT "(" [FuncFParams] ")" Block;
FuncType      ::= "void" | "int";
FuncFParams   ::= FuncFParam {"," FuncFParam};
FuncFParam    ::= BType IDENT;

Block         ::= "{" {BlockItem} "}";
BlockItem     ::= Decl | Stmt;

Stmt          ::= OpenStmt | ClosedStmt
OpenStmt      ::= OpenIf | OpenWhile
ClosedStmt    ::= SimpleStmt | ClosedIf | ClosedWhile
OpenIf        ::= "if" "(" Exp ")" Stmt
                | "if" "(" Exp ")" ClosedStmt "else" OpenStmt
ClosedIf      ::= "if" "(" Exp ")" ClosedStmt "else" ClosedStmt
OpenWhile     ::= "while" "(" Exp ")" OpenStmt
ClosedWhile   ::= "while" "(" Exp ")" ClosedStmt
SimpleStmt    ::= LVal "=" Exp ";"
                | [Exp] ";"
                | Block
                | "break" ";"
                | "continue" ";"
                | "return" [Exp] ";";

Decl          ::= ConstDecl | VarDecl;
ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
BType         ::= "int";
ConstDef      ::= IDENT "=" ConstInitVal;
ConstInitVal  ::= ConstExp;
VarDecl       ::= BType VarDef {"," VarDef} ";";
VarDef        ::= IDENT | IDENT "=" InitVal;
InitVal       ::= Exp;

Exp           ::= EqExp;
ConstExp      ::= Exp;
PrimaryExp    ::= "(" Exp ")" | LVal | Number | CallExp;
LVal          ::= IDENT;
Number        ::= INT_CONST;
UnaryExp      ::= PrimaryExp | UnaryOp UnaryExp;
UnaryOp       ::= "+" | "-" | "!";
MulExp        ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
AddExp        ::= MulExp | AddExp ("+" | "-") MulExp;
CallExp       ::= IDENT "(" [FuncRParams] ")"
FuncRParams   ::= Exp {"," Exp};

RelExp        ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
EqExp         ::= RelExp | EqExp ("==" | "!=") RelExp;
